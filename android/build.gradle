buildscript {
    ext {
        buildToolsVersion = "34.0.0"
        minSdkVersion = 24
        compileSdkVersion = 34
        targetSdkVersion = 34
        supportLibVersion = "33.0.0"
        kotlinVersion = "1.8.21"
        kotlin_version = kotlinVersion
        RNNKotlinVersion = kotlinVersion
        firebaseVersion = "23.3.1"
        ndkVersion = "25.1.8937393"
    }
    repositories {
        mavenCentral()
        mavenLocal()
        google()
    }
    dependencies {
        classpath("com.android.tools.build:gradle")
        classpath("com.facebook.react:react-native-gradle-plugin")
        classpath('com.google.gms:google-services:4.4.0')
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion")

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }

    configurations.classpath {
        resolutionStrategy.activateDependencyLocking()
    }
}

allprojects {
    repositories {
        maven {
            url "$rootDir/../node_modules/detox/Detox-android"
        }
    }
}

subprojects {
    afterEvaluate { subproject ->
        if(subproject['name'] == 'react-native-create-thumbnail'){
            def myAttribute = Attribute.of("com.android.build.api.attributes.BuildTypeAttr", String)
            dependencies.attributesSchema {
                attribute(myAttribute)
            }
            configurations {
                implementation {
                    attributes {
                        attribute(myAttribute, "release")
                    }
                }
            }
        }
    }
}

apply plugin: "com.facebook.react.rootproject"

dependencyLocking {
    lockAllConfigurations()
}
